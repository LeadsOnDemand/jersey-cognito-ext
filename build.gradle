buildscript {

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }

}

plugins {
    id 'maven'
}

apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'net.linguica.maven-settings'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.10
targetCompatibility = 1.10

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://dl.bintray.com/anigenero/maven'
    }
}

group 'com.anigenero.jersey'
version '1.0-SNAPSHOT'

ext {
    jerseyExtVersion = '2.27'
    junitVersion = '5.2.+'
}

dependencies {

    compileOnly group: 'javax.enterprise', name: 'cdi-api', version: '2.0'
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.1'

    compile group: 'com.anigenero.cdi', name: 'configuration-provider', version: '1.0.0'

    compile 'javax.ws.rs:javax.ws.rs-api:2.1@jar'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    compile(group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: jerseyExtVersion) {
        exclude group: 'javax.ws.rs'
    }
    compile(group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: jerseyExtVersion){
        exclude group: 'javax.ws.rs'
    }
    compile(group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: jerseyExtVersion) {
        exclude group: 'javax.ws.rs'
    }
    compile(group: 'org.glassfish.jersey.ext', name: 'jersey-declarative-linking', version: jerseyExtVersion) {
        exclude group: 'javax.ws.rs'
    }
    compile(group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: jerseyExtVersion) {
        exclude group: 'javax.ws.rs'
    }
    compile(group: 'org.glassfish.jersey.ext.cdi', name: 'jersey-cdi1x-servlet', version: jerseyExtVersion) {
        exclude group: 'javax.ws.rs'
    }
    compile(group: 'org.glassfish.jersey.ext', name: 'jersey-bean-validation', version: jerseyExtVersion) {
        exclude group: 'javax.ws.rs'
        exclude group: 'org.hibernate'
    }

    compile group: 'com.auth0', name: 'java-jwt', version: '3.4.+'
    compile group: 'com.auth0', name: 'jwks-rsa', version: '0.6.+'

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testCompile group: 'javax.enterprise', name: 'cdi-api', version: '2.0'

    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.+'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.10.+'

    testCompile group: 'com.anigenero.junit', name: 'junit-mockito-extension', version: '1.0.1'

}

test {

    useJUnitPlatform()

    jacoco {
        excludes = ['**/Test*']
        append = false
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }

}

project.task(type: JacocoReport, "jacocoJupTestReport") {

    executionData test
    sourceSets(sourceSets.main)

    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)

    reports {
        xml.enabled true
        csv.enabled false
        html.destination "$buildDir/jacoco/html"
    }

}

project.task(type: JacocoCoverageVerification, "jacocoTestVerify") {

    executionData test

    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)

    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }

}

check.dependsOn jacocoJupTestReport

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava', 'sourcesJar']
    pkg {
        repo = 'maven'
        name = 'configuration-provider'
        userOrg = System.getenv('BINTRAY_USERNAME')
        licenses = ['MIT']
        vcsUrl = 'https://github.com/anigenero/jersey-cognito-ext.git'
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

def pomConfig = {

    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "anigenero"
            name "Robin Schultz"
            email "robin@anigenero.com"
        }
    }

    scm {
        url "https://github.com/anigenero/jersey-cognito-ext"
    }

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourceJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Jersey auth provider for AWS Cognito')
                root.appendNode('name', 'jersey-cognito-ext')
                root.children().last() + pomConfig
            }
        }
    }
}
